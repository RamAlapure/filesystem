plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "org.sonarqube" version "3.3"
}

// gradle attributes
group = project.property('groupId')
version = '1.2-SNAPSHOT'

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.11.579'
    }
}

dependencies {

    // The apache common utils for file and string handling
    implementation group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'

    // The dependency is used for dynamically creating dependancies at run-time.
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.11'

    // The dependency to avoid writing boiler plate code
    implementation 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    // AWS dependency
    implementation 'com.amazonaws:aws-java-sdk-s3'

    // SFTP client dependency
    implementation group: 'com.jcraft', name: 'jsch', version: '0.1.55'

    // Use JUnit test framework
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.5.1'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.5.1'

}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = project.property('artifactId')
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                packaging = 'jar'
                name = project.property('name')
                description = project.property('description')
                url = project.property('gitUrl')
                licenses {
                    license {
                        name = project.property('licenseName')
                        url = project.property('licenseUrl')
                    }
                }
                developers {
                    developer {
                        id = project.property('developerId')
                        name = project.property('developerName')
                        email = project.property('developerEmail')
                    }
                }
                scm {
                    connection = project.property('scmConnection')
                    developerConnection = project.property('scmDeveloperConnection')
                    url = project.property('gitUrl')
                }
            }
        }
    }
    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ? project.property('snapshotsRepoUrl') : project.property('releasesRepoUrl')
            credentials {
                username = project.property('nexusUsername')
                password = project.property('nexusPassword')
            }
        }
    }
}

sonarqube {
  properties {
    property "sonar.projectKey", "RamAlapure_filesystem"
    property "sonar.organization", "ramalapure"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
